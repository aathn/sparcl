def (.) f g = \x -> f (g x)
fixity . 90 right

def ($) = \f x -> f x
fixity $ 0 right

sig id : a # p -> a
def id x = x

sig fst : (a, b) -> a
def fst (x, _) = x

sig snd : (a, b) -> b
def snd (_, x) = x

sig inv : (rev a -o rev b) -> rev b -o rev a
def inv f = let (x, y) <- unlift f in lift y x

sig conj : (rev a -o rev b) -> (rev b -o rev b) -o rev a -o rev a
def conj f g = inv f . g . f

sig fwd : (rev a -o rev b) -> a -> b
def fwd = fst . unlift

sig bwd : (rev a -o rev b) -> b -> a
def bwd = snd . unlift

type Eq p q a = a # p -> a # q -> Bool

sig new : Eq p q a -> a -> rev a
def new eq c =
  lift (\() -> c)
       (\c' -> let True <- eq c c' in ())
       (rev ())

sig del : Eq p q a -> a -> rev a -o rev ()
def del eq c = inv (\(rev ()) -> new eq c)

sig copy : Eq p q a -> rev a -o rev (a, a)
def copy eqA a = pin a (new eqA)

sig mapPair : (rev a -o rev c) -o (rev b -o rev d) -o rev (a, b) -o rev (c, d)
def mapPair f g (rev (x, y)) = rev (f x, g y)

sig mapFst : (rev a -o rev b) -o rev (a, c) -o rev (b, c)
def mapFst f = mapPair f id

sig mapSnd : (rev a -o rev b) -o rev (c, a) -o rev (c, b)
def mapSnd f = mapPair id f

sig swap : rev (a, b) -o rev (b, a)
def swap (rev (a, b)) = rev (b, a)

sig pairr : rev a -o rev b -o rev (b, a)
def pairr a b = rev (b, a)

sig pairl : rev a -o rev b -o rev (a, b)
def pairl a = swap . pairr a

sig unitr : rev (x, ()) -o rev x
def unitr (rev (x, ())) = x

sig unitl : rev ((), x) -o rev x
def unitl = unitr . swap

sig assocr : rev ((a, b), c) -o rev (a, (b, c))
def assocr (rev ((a, b), c)) = rev (a, rev (b, c))

sig assocl : rev (a, (b, c)) -o rev ((a, b), c)
def assocl = inv assocr

data Either a b = Left a | Right b

sig isLeft : Either a b -> Bool
def isLeft (Left _) = True
         | _        = False

sig eitherToBool : rev (Either a a) -o rev (a, Bool)
def eitherToBool (rev Left  a) = rev (a, rev True) with snd
               | (rev Right a) = rev (a, rev False)

sig mirror : rev (Either a b) -o rev (Either b a)
def mirror (rev Right a) = rev Left a with isLeft
         | (rev Left a)  = rev Right a

sig distr : rev (Either a b, c) -o rev (Either (a, c) (b, c))
def distr (rev (Left  a, c)) = rev Left  rev (a, c) with isLeft
        | (rev (Right b, c)) = rev Right rev (b, c)

sig mapEither
  : (rev a -o rev c) -> (rev b -o rev d) -> rev (Either a b) -o rev (Either c d)
def mapEither f g (rev Left a)  = rev Left  (f a) with isLeft
            | f g (rev Right b) = rev Right (g b)

sig mapLeft  : (rev a -o rev b) -> rev (Either a c) -o rev (Either b c)
def mapLeft  f = mapEither f id

sig mapRight : (rev a -o rev b) -> rev (Either c a) -o rev (Either c b)
def mapRight f = mapEither id f

data List a = Cons a (List a) | Nil

sig null : List a -> Bool
def null Nil = True
       | _   = False

type Reader c x y = rev (c, x) -o rev (c, y)

sig arr : (rev a -o rev b) -o Reader c a b
def arr = mapSnd

sig (>>>) : Reader d a b -o Reader d b c -o Reader d a c
def (>>>) ra rb = rb . ra

sig first : Reader c a b -o Reader c (a, d) (b, d)
def first ra (rev (c, (a, d))) =
  let rev (c, b) <- ra rev (c, a) in
  rev (c, rev (b, d))

sig second : Reader c a b -o Reader c (d, a) (d, b)
def second ra = arr swap >>> first ra >>> arr swap

sig arrInv : Reader c a b -> Reader c b a
def arrInv = inv

sig left : Reader c a b -> Reader c (Either a d) (Either b d)
def left ra (rev (c, Left a)) =
         let rev (c, b) <- ra rev (c, a) in
         rev (c, rev Left b)
         with isLeft . snd
       | ra (rev (c, Right d)) = rev (c, rev Right d)

sig right : Reader c a b -> Reader c (Either d a) (Either d b)
def right ra = arr mirror >>> left ra >>> arr mirror

sig (+++) : Reader e a c -> Reader e b d -> Reader e (Either a b) (Either c d)
def (+++) ra rb = left ra >>> right rb

sig arrCase :
  Reader d c (c, Bool) -> Reader d a c -> Reader d b c -> Reader d (Either a b) c
def arrCase rbranch ra rb =
  (ra +++ rb) >>> arr eitherToBool >>> arrInv rbranch

sig caseR : Reader c1 a b -> Reader c2 a b -> Reader (Either c1 c2) a b
def caseR r1 r2 = inv distr . mapEither r1 r2 . distr

data GFun a b where
  sig GF : (rev a -o rev (g, b)) -o GFun a b
end

sig arrG : (rev a -o rev b) -o GFun a b
def arrG f = GF (inv unitl . f)

sig (>>!) : GFun a b -o GFun b c -o GFun a c
def (>>!) (GF fa) (GF fb) = GF (assocl . mapSnd fb . fa)

sig firstG : GFun a b -o GFun (a, d) (b, d)
def firstG (GF fa) = GF (assocr . mapFst fa)

sig secondG : GFun a b -o GFun (d, a) (d, b)
def secondG fa = arrG swap >>! firstG fa >>! arrG swap

sig caseG : GFun a c -> GFun b c -> GFun (Either a b) c
def caseG (GF fa) (GF fb) = GF (inv distr . mapEither fa fb)

sig erase : GFun a ()
def erase = GF (inv unitr)

sig revBool : Bool -o rev Bool
def revBool True  = rev True
          | False = rev False

sig liftP' : (a # p -> Bool) -> rev a -o rev (a, Bool)
def liftP' p a = pin a (revBool . p)

sig liftP : (a # p -> Bool) -> Reader c a (a, Bool)
def liftP = arr . liftP'

sig liftPR : ((c, a) # p -> Bool) -> Reader c a (a, Bool)
def liftPR p = assocr . liftP' p

sig localR : (rev s -o rev s') -> Reader s' a b -o Reader s a b
def localR f r = mapFst (inv f) . r . mapFst f

sig extendR : Reader s a b -o Reader (g, s) a b
def extendR r = assocl . mapSnd r . assocr

sig mutateR : GFun s s' -> Reader s' a b -o Reader s a b
def mutateR (GF f) = localR f . extendR

sig shrinkR : Reader (s, s') a b -o Reader s (s', a) (s', b)
def shrinkR r = assocr . r . assocl

sig updateR : GFun (s, a) s' -> Reader s' b c -o Reader s (a, b) (a, c)
def updateR gf = shrinkR . mutateR gf

sig pinR : Eq p q s' -> GFun (s, a) s' -> Reader s a (a, s')
def pinR eqS' gf = arr (inv unitr) >>> updateR gf (ask eqS') >>> second (arr unitr)

sig liftR : Reader c a b -o Reader d (c, a) (c, b)
def liftR = shrinkR . extendR

sig ask : Eq p q c -> Reader c a (c, a)
def ask eq = assocr . mapFst (copy eq)

sig apR : Reader c x y -o rev c -> rev x -o rev y
def apR r c = inv (pairl c) . r . pairl c

sig fromReader : Eq p q c -> Reader c x y -o c -> rev x -o rev y
def fromReader eqC r c x = let c' <- new eqC c in apR r c' x

sig toReader : (c -> rev x -o rev y) -o Reader c x y
def toReader f (rev (c, x)) = pin c (\c' -> f c' x)

sig add : Reader Int Int Int
def add = lift (\(m, n) -> (m, n + m)) (\(m, n) -> (m, n - m))

sig sub : Reader Int Int Int
def sub = arrInv add

sig succ : rev Int -o rev Int
def succ = apR add (new eqInt 1)

sig pred : rev Int -o rev Int
def pred = inv succ

sig is0 : rev Int -o rev (Either () Int)
def is0 =
  mapLeft (del eqInt 0) . inv eitherToBool . liftP' (eqInt 0)

-- sig goSubs : Reader Int (List Int) (List Int)
-- def goSubs = proc l -> do
--   case l of
--   | rev Nil -> returnA -< rev Nil with null
--   | rev (Cons x xs) -> do
--     rev (x, r) <- liftR goSubs -< rev (x, xs)
--     x' <- sub -< x
--     returnA -< rev Cons x' r
--   end

-- sig goSubs : Reader Int (List Int) (List Int)
-- def goSubs = *\l ->
--   case l of
--   | rev Nil -> [rev Nil] with null
--   | rev (Cons x xs) ->
--     *let rev (x, r) <- liftR goSubs * [rev (x, xs)] in
--     *let rev x' <- sub * [x] in
--     [rev Cons x' r]
--   end

sig goSubs : Reader Int (List Int) (List Int)
def goSubs =
  arr uncons >>>
  arrCase (liftP null)
    (arr (\(rev ()) -> rev Nil))
    (liftR goSubs >>>
      first sub >>>
        arr (\(rev (x, r)) -> rev Cons x r))

sig subs : rev (List Int) -o rev (List Int)
def subs = apR goSubs (new eqInt 0)

sig uncons : rev (List a) -o rev (Either () (a, List a))
def uncons (rev Nil)         = rev Left  rev () with isLeft
         | (rev (Cons x xs)) = rev Right rev (x, xs)

data Tree = L | N Int Tree Tree

sig piR : rev Tree -o rev (List Int, List Int)
def piR (rev L)       = rev (rev Nil, rev Nil) with null . fst
      | (rev N a l r) =
        let rev (pr, ir)      <- piR r in
        let rev (a, (pl, il)) <- piAccSR rev (a, rev (l, rev (pr, ir))) in
        rev (rev Cons a pl, il)

-- sig piAccSR : Reader Int (Tree, (List Int, List Int)) (List Int, List Int)
-- def piAccSR = proc (rev (t, pyiy)) -> do
--   case t of
--   | rev L -> do
--     rev (h, (py, iy)) <- ask eqInt -< pyiy
--     returnA -< rev (py, rev Cons h iy)
--     with liftPR $ \(h1, (_, Cons h2 _)) -> eqInt h1 h2
--   | rev (N a l r) -> do
--     rev prir          <- piAccSR -< rev (r, pyiy)
--     rev (a, (pl, il)) <- liftR piAccSR -< rev (a, rev (l, prir))
--     returnA -< rev (rev Cons a pl, il)
--   end

sig piAccSR : Reader Int (Tree, (List Int, List Int)) (List Int, List Int)
def piAccSR =
  first (arr untree) >>> arr distr >>>
  arrCase (liftPR $ \(h1, (_, Cons h2 _)) -> eqInt h1 h2)
    (arr unitl >>> ask eqInt >>>
     arr (\(rev (h, (py, iy))) -> rev (py, rev Cons h iy)))
    (arr assocr >>> second piAccSR >>>
     arr assocr >>> liftR piAccSR >>>
     arr (\(rev (a, (pl, il))) -> rev (rev Cons a pl, il)))

sig untree : rev Tree -o rev (Either () ((Int, Tree), Tree))
def untree (rev L)         = rev Left  rev () with isLeft
         | (rev (N a l r)) = rev Right rev (rev (a, l), r)

type Symbol = Char
data Huff   = Lf Symbol | Br Huff Huff

sig member : Symbol -> Huff -> Bool
def member s (Lf s')  = eqChar s s'
         | s (Br l r) =
           case member s l of
           | True  -> True
           | False -> member s r
           end

sig eqHuff : Huff -> Huff -> Bool
def eqHuff (Lf s)   (Lf s') = eqChar s s'
        |  (Br l r) (Br l' r') =
          case eqHuff l l' of
          | True  -> eqHuff r r'
          | False -> False
          end
        |  _         _ = False

sig unhuff : rev Huff -o rev (Either Symbol (Huff, Huff))
def unhuff (rev Lf s)   = rev Left  s          with isLeft
         | (rev Br l r) = rev Right rev (l, r)

data Bit = Z | O

sig unbit : rev (List Bit) -o rev (Either (List Bit) (List Bit))
def unbit (rev Cons Z l) = rev Left  l with isLeft
        | (rev Cons O l) = rev Right l

-- sig decR : Reader Huff (List Bit) (Symbol, List Bit)
-- def decR = proc ys -> do
--   caseR of
--   | Lf _   -> ask eqChar -< ys
--   | Br _ _ ->
--     case ys of
--     | rev (Cons Z ys') -> do
--       res <- mutateR (GF swap) decR -< ys'
--       returnA -< res with liftPR $ \((l, _), (s, _)) -> member s l
--     | rev (Cons O ys') -> do
--       res <- mutateR (GF id) decR -< ys'
--       returnA -< res
--     end
--   end

sig decR : Reader Huff (List Bit) (Symbol, List Bit)
def decR =
  localR unhuff $
    caseR
      (ask eqChar)
      (arr unbit >>>
       arrCase (liftPR $ \((l, _), (s,_)) -> member s l)
         (mutateR (GF swap) decR)
         (mutateR (GF id)   decR))

sig encR : Reader Huff (Symbol, List Bit) (List Bit)
def encR = arrInv decR

-- sig encode : Reader Huff (List Symbol) (List Bit)
-- def encode = proc l ->
--   case l of
--   | rev Nil       -> returnA -< rev Nil with null
--   | rev Cons s ss -> do
--       rest <- encode -< ss
--       encR -< rev (s, rest)
--   end

sig encode : Reader Huff (List Symbol) (List Bit)
def encode =
  arr uncons >>>
  arrCase (liftP null)
    (arr (\(rev ()) -> rev Nil))
    (second encode >>> encR)

sig dummyHuff : Huff
def dummyHuff = Br (Lf 'a') (Br (Lf 'b') (Br (Lf 'c') (Lf 'd')))

sig makeHuff : rev (List Symbol) -o rev (List Symbol, Huff)
def makeHuff s = rev (s, new eqHuff dummyHuff)

sig huffCompress : rev (List Symbol) -o rev (Huff, List Bit)
def huffCompress = encode . swap . makeHuff


sig and : Bool -> Bool -> Bool
def and True True = True
      | _    _    = False

sig eqPair : Eq p q a -> Eq s t b -> (a, b) -> (a, b) -> Bool
def eqPair eqA eqB (a1, b1) (a2, b2) =
  case eqA a1 a2 of
  | True -> eqB b1 b2
  | False -> False
  end

data LZPair = Lit Symbol | Pair (Int, Int)

sig isLit : LZPair -> Bool
def isLit (Lit _) = True
        | _       = False

sig head : List a -> a
def head (Cons h _) = h

sig tail : List a -> List a
def tail (Cons _ t) = t

sig length : List a -> Int
def length Nil        = 0
         | (Cons _ t) = 1 + length t

sig lengthR : Reader (List a) () Int
def lengthR =
  localR uncons $
    caseR
      (arr (\(rev ()) -> new eqInt 0))
      (mutateR (GF id) lengthR >>> arr succ)

sig eqList : Eq p q a -> List a -> List a -> Bool
def eqList eq Nil Nil = True
         | eq (Cons h1 t1) (Cons h2 t2) =
           case eq h1 h2 of
           | True  -> eqList eq t1 t2
           | False -> False
           end
         | eq _ _     = False

data Maybe a = Nothing | Just a

sig isNothing : Maybe a -> Bool
def isNothing Nothing = True
            | _       = False

sig eqMaybe : Eq p q a -> Maybe a -> Maybe a -> Bool
def eqMaybe eq Nothing  Nothing  = True
          | eq (Just x) (Just y) = eq x y
          | eq _        _        = False

sig unmaybe : rev (Maybe a) -o rev (Either () a)
def unmaybe (rev Nothing) = rev Left rev () with isLeft
          | (rev Just x)  = rev Right x

sig winLength : Int
def winLength = 2048

sig lz77 : rev (List Symbol) -o rev (List LZPair)
def lz77 input =
  let rev (Nil, output) <- goLz77 rev (rev Nil, input) in output

-- sig goLz77 : Reader (List Symbol) (List Symbol) (List LZPair)
-- def goLz77 = proc l ->
--   case l of
--   | rev Nil      -> rev Nil with null
--   | rev Cons h t ->
--     rev (pair, (h, t)) <- maxMatch -< rev (h, t)
--     case pair of
--     | rev Nothing ->
--       rev (oneH, lzRest) <- updateR updateWindow goLz77 -< rev (rev (new eqInt 1, rev Cons h rev Nil), t)
--       let rev (Cons h Nil) <- inv (pairl (new eqInt 1)) oneH in
--       returnA -< rev Cons (rev Lit h) lzRest
--       with isLit . head
--     | rev Just (d, l) ->
--       rev (l, (match, rest))   <- liftR split -< rev (l, rev Cons h t)
--       rev ((l, match), lzRest) <- updateR updateWindow goLz77 -< rev (rev (l, match), rest)
--       p <- arrInv (pinR (eqList eqChar) getMatch) -< rev (rev (d, l), match)
--       returnA -< rev Cons (rev Pair p) lzRest
--     end
--   end

sig goLz77 : Reader (List Symbol) (List Symbol) (List LZPair)
def goLz77 =
  arr uncons >>>
  arrCase (liftP null)
    (arr (\(rev ()) -> rev Nil))
    (maxMatch >>> first (arr unmaybe) >>> arr distr >>>
     arrCase (liftP $ isLit . head)
      (arr unitl >>> first (arr (\h -> rev Cons h rev Nil)) >>>
       arr (assocl . pairl (new eqInt 1)) >>>
       updateR updateWindow goLz77 >>> arr (inv (pairl (new eqInt 1)) . assocr) >>>
       arr (\(rev (Cons h Nil, lzRest)) -> rev Cons (rev Lit h) lzRest))
      (second (arr (\(rev (h, t)) -> rev Cons h t)) >>> arr assocr >>>
       second (liftR split >>> arr assocl >>> updateR updateWindow goLz77) >>>
       arr assocl >>> first (arr assocl >>> arrInv (pinR (eqList eqChar) getMatch)) >>>
       arr (\(rev (p, lzRest)) -> rev Cons (rev Pair p) lzRest)))

sig updateWindow : GFun (List Symbol, (Int, List Symbol)) (List Symbol)
def updateWindow = GF $ \(rev (window, (len, match))) ->
  let wl <- new eqInt winLength in
  let rev (len, (winit, wlast)) <-
    mutateR (GF $ sub . pairr wl) split rev (len, window) in
  let rev (len, newWin) <- concat rev (len, rev (reverse match, winit)) in
  rev (rev (len, wlast), newWin)

sig maxMatch : Reader (List Symbol) (Symbol, List Symbol) (Maybe (Int, Int), (Symbol, List Symbol))
def maxMatch wi =
  let rev ((window, input), pair) <-
    pin wi (\(window, (h, t)) -> new eqMaybeIntPair (maxMatch' window (Cons h t))) in
  rev (window, rev (pair, input))
  where
    def eqMaybeIntPair = eqMaybe (eqPair eqInt eqInt)
    def maxMatch' window input =
      let (d, l) <- goMaxMatch (0, 0) Nil window input in
        case eqInt 0 l of
          | True  -> Nothing
            | False -> Just (d, l)
        end

    def goMaxMatch bestMatch matchBuf Nil        input = bestMatch
                 | bestMatch matchBuf (Cons h t) input =
                   let matchBuf <- Cons h matchBuf in
                   let newMatch <- updateMatch bestMatch matchBuf input in
                   goMaxMatch newMatch matchBuf t input

    def updateMatch (d, l) matchBuf input =
      let newLength <- length (createMatch matchBuf matchBuf input) in
      case ltInt l newLength of
      | True -> (length matchBuf, newLength)
      | False -> (d, l)
      end

    def createMatch matchBuf Nil          input        = createMatch matchBuf matchBuf input
                  | matchBuf _            Nil          = Nil
                  | matchBuf (Cons h1 t1) (Cons h2 t2) =
                    case eqChar h1 h2 of
                    | True -> Cons h1 (createMatch matchBuf t1 t2)
                    | False -> Nil
                    end
  end

-- def split = proc l ->
--   case l of
--   | rev Nil      ->
--     returnA -< rev (rev Nil, rev Nil)
--     with \(x, y) -> and (null x) (null y)
--   | rev Cons h t ->
--     localR is0 $
--       caseR of
--       | Left  _ -> returnA -< rev (rev Nil, rev Cons h t)
--       | Right _ -> do
--         rev (hs, ts) <- localR pred split -< t
--         returnA -< rev (rev Cons h hs, ts)
--       end
--   end

sig split : Reader Int (List Symbol) (List Symbol, List Symbol)
def split =
  arr uncons >>>
  arrCase (liftP $ \(x, y) -> and (null x) (null y))
    (arr (\(rev ()) -> rev (rev Nil, rev Nil)))
    (localR is0 $
      caseR
        (arr (\(rev (h, t)) -> rev (rev Nil, rev Cons h t)))
        (second (localR pred split) >>>
         arr (\(rev (h, (hs, ts))) -> rev (rev Cons h hs, ts))))

sig concat : Reader Int (List Symbol, List Symbol) (List Symbol)
def concat = arrInv split

sig reverse : rev (List Symbol) -o rev (List Symbol)
def reverse l =
  let rev (l, len)  <- lengthR rev (l, rev ()) in
  let rev (Nil, l') <- goReverse len l in l'
  where
    def goReverse n l =
      case is0 n of
      | rev Left  () -> rev (l, rev Nil) with null . snd
      | rev Right n  ->
        let rev (Cons h t, l') <- goReverse (pred n) l in
        rev (t, rev Cons h l')
      end
  end

-- sig getMatch : GFun (List Symbol, (Int, Int)) (List Symbol)
-- def getMatch = proc (rev (window, (d, l))) ->
--   matchBuf <- revTake -< rev (d, window)
--   repeatTake -< rev (l, matchBuf)

sig getMatch : GFun (List Symbol, (Int, Int)) (List Symbol)
def getMatch =
  arrG (mapFst swap . assocl) >>! firstG revTake >>!
  arrG swap >>! repeatTake

-- sig repeatTake : GFun (Int, List Symbol) (List Symbol)
-- def repeatTake = proc (rev (n, l)) -> goRepeatTake -< rev (n, copy (eqList eqChar) l)
--   where
--     def goRepeatTake = proc (rev (n, (l1, l2))) ->
--       case l2 of
--       | Nil      -> goRepeatTake -< rev (n, copy (eqList eqChar) l1)
--       | Cons h t ->
--         case is0 n of
--         | Left  () ->
--           rev () <- erase -< rev (l1, (h, t))
--           returnA -< rev Nil
--         | Right n  -> do
--           rest <- goRepeatTake -< rev (pred n, rev (l1, t))
--           returnA -< rev Cons h rest
--         end
--       end
--   end

sig repeatTake : GFun (Int, List Symbol) (List Symbol)
def repeatTake = secondG (arrG $ copy (eqList eqChar)) >>! goRepeatTake
  where
    def goRepeatTake =
      arrG (swap . assocl) >>! firstG (arrG uncons) >>! arrG distr >>!
      caseG
        (arrG unitl >>! secondG (arrG $ copy (eqList eqChar)) >>! goRepeatTake')
        (arrG (distr . mapFst is0 . assocr . swap) >>!
          caseG
            (erase >>! arrG (\(rev ()) -> rev Nil))
            (arrG (swap . assocl . mapSnd (assocl . mapSnd swap)) >>!
             secondG (firstG (arrG pred) >>! goRepeatTake') >>!
             arrG (\(rev (h, rest)) -> rev Cons h rest)))

    def goRepeatTake' = GF (\x -> let GF f <- goRepeatTake in f x)
  end

-- sig revTake : GFun (Int, List a) (List a)
-- def revTake = proc (rev (n, l)) -> goRevTake -< rev (n, rev (l, rev Nil))
--   where
--     def goRevTake = proc (rev (n, (l, acc))) ->
--       case l of
--       | Nil ->
--         rev () <- erase -< n
--         returnA -< acc
--       | Cons h t ->
--         case is0 n of
--         | Left () ->
--           rev () <- erase -< rev (h, t)
--           returnA -< acc
--         | Right n ->
--           goRevTake -< rev (pred n, rev (t, rev Cons h acc))
--         end
--       end
--   end

sig revTake : GFun (Int, List a) (List a)
def revTake = arrG (assocr. pairr (rev Nil)) >>! goRevTake
  where
    def goRevTake =
      arrG (distr . mapFst uncons . assocr . swap) >>!
      caseG
        (arrG unitl >>! secondG erase >>! arrG unitr)
        (arrG (distr . mapFst is0 . swap . assocl) >>!
          caseG
            (arrG unitl >>! firstG erase >>! arrG unitl)
            (arrG (mapSnd (mapSnd (\(rev (h, acc)) -> rev Cons h acc)
                           . assocr . mapFst swap) . mapFst pred) >>!
             goRevTake'))

    def goRevTake' = GF (\x -> let GF f <- goRevTake in f x)
  end
